{//; changelog
2018-08-??: added a warning for corps that are not safe to join or leave due to WS
2018-08-13: added a message if the user ia also in other RS queues
2018-09-22: added global var for RS participants
2018-10-28: added vars to switch autostart on full queue on/off.
2018-11-06: added icons for current WS
2018-12-14: added RS count tiers
2018-12-17: rework to keep the channel cleaner - keep a single up-to-date queue for each RS#
			if the user has only one rs-tag, assume the rs# if not given as arg
2019-06-06: jordan - bring the ~pinglimit in-line with the bot warnings
2019-07-18: multiRS (use global arrays _RSQ-channelIDs, _RSQ-maxMembers, _RSQ-autostartAt, _RSQ-levels, _RDQ-names)
}
{//; defaults}
{set;~allowed;["415694197723496458","482308841975971840"]}{//; bso-rs, dbg}
{set;~autostart;true}
{set;~maxIn;4}{if;0{get;_rsMembers};{set;~maxIn;{get;_rsMembers}}}
{set;~rsA;{get;_rsLevels}}
{set;~queueName;{channelname;{channelid}}}
{set;rsTiers;{get;_rsCountTiers}}
{if;{length;{get;_ping_warn_limit}};{set;~pinglimit;{get;_ping_warn_limit}};{set;~pinglimit;300}}
{//; check for custom data}
{if;{length;{get;_RSQ-channelIDs}};>;0;
  	{set;~allowed;{get;_RSQ_channelIDs}}
	{set;~RSQ-index;{indexof;{get;_RSQ_channelIDs};{channelid}}}
	{if;{get;~RSQ-index};<;0;{return}}
	{if;{length;{get;_RSQ-autostartAt;{get;~RSQ-index}}};>;0;{set;~autostart;{get;_RSQ-autostartAt;{get;~RSQ-index}}}}
	{if;{length;{get;_RSQ-maxMembers;{get;~RSQ-index}}};>;0;{set;~maxIn;{get;_RSQ-maxMembers;{get;~RSQ-index}}}}
	{if;{length;{get;_RSQ-levels;{get;~RSQ-index}}};>;0;{set;~rsA;{split;{get;_RSQ-levels;{get;~RSQ-index}};,}}}
	{if;{length;{get;_RSQ-names;{get;~RSQ-index}}};>;0;{set;~queueName;{get;_RSQ-names;{get;~RSQ-index}}}}
}
{if;{indexof;{get;~allowed};{channelid}};==;-1;{return}}
{delete}

{set;~startmsg;[]}
	{push;~startmsg;The queue is full, starting in 3, 2, 1..}
	{push;~startmsg;Ready to roll? Clap your hands and count with me: 1, 2, go!}

{set;uid;{userid}}
{set;~rsN;}
{if;{argslength};==;0;{//; rsN not given, check if we can assume it}
	{set;userRStags;[]}
	{foreach;~rs;~rsA;{if;{userhasrole;{roleid;RS{get;~rs};quiet};{get;uid}};==;true;{push;userRStags;{get;~rs}}}}
	{if;{length;{get;userRStags}};==;1;
      	{set;~rsN;{get;userRStags;0}}
      	;
      	{usernick}, You appear to have more than one RS-tag, please specify a valid RS __**number**__
      	{return}
	}
	;
	{set;~rsN;{trim;{execcc;parsenum;{args;0}}}}
}
{if;{indexof;{get;~rsA};{get;~rsN}};==;-1;{usernick}, "{get;~rsN}" is not a valid RS __**number**__{return}}

{if;{logic;||;{bool;{isarray;{get;_rsQ{channelid}{get;~rsN}}};==;false};{bool;{length;{get;_rsQ{channelid}{get;~rsN}}};==;0}};
  {set;_rsQ{channelid}{get;~rsN};[]}
  {set;_rsQ{channelid}{get;~rsN}_start;{time;X}}
}
{if;{get;_rs{get;~rsN}lock};==;true;
{//;  {void;{send;482308841975971840;`{commandname} {join;{argsarray};{space}}` called by `{usernick}` while locked, pausing...}}
}  
  {sleep;3s}
}
{set;_rs{get;~rsN}lock;true}{commit}
{//; (temporarily removed lock)
{lock;write;in;{//; to prevent another in while this one is not finished}
}
}
{set;inmsg;[]}
{set;counterIn;{length;{get;_rsQ{channelid}{get;~rsN}}}}
{set;userIndex;{indexof;{get;_rsQ{channelid}{get;~rsN}};{get;uid}}}

{if;{get;userIndex};!=;-1;
	{push;inmsg;{usermention}, you are in for __**RS{get;~rsN}!**__ ({get;counterIn}/{get;~maxIn})}
	;{//; not in queue}
	{if;{get;counterIn};==;{get;~maxIn};{usermention}, the queue is full{return}}
}
{//; update these, intimers depends on them to work correctly}
{set;_lastIn{channelid}{get;uid}{get;~rsN};{time;x}}
{set;~thisIn;{get;_lastIn{channelid}{get;uid}{get;~rsN}}}

{set;userWSicons;{trim;{execcc;wsfuncs;icon;{get;uid}}}}
{set;rsModIcons;}{//;{trim;{execcc;rsmod;icon;{get;uid}}}}{//; will contain icons for select mods - barrage, veng, etc.}
{set;inQueuesMsg;}
{set;rsTier;}
{set;~start;false}
{if;{get;userIndex};==;-1;{//; user not in queue, add them}
	{push;{get;_rsQ{channelid}{get;~rsN}};{get;uid}}
  	{commit}
	{set;counterIn;{length;{get;_rsQ{channelid}{get;~rsN}}}}
  	{//; ping RS or not}
	{set;~rsText;__**RS{get;~rsN}**__}
	{set;~ping;{time;X;{get;_last_RS{get;~rsN}_mentioned}}}
               {//; old: {get;_lastRS{get;~rsN}Ping} }
	{set;~diff;{math;-;{time;X};{get;~ping}}}
	{if;{logic;&&;{bool;{get;~diff};>;{get;~pinglimit}};
  				  {bool;{get;counterIn};<;{get;~maxIn}};
      			  {bool;{roleid;RS{get;~rsN};quiet};!=;}
    	};
  		{set;~rsText;{rolemention;RS{get;~rsN}}}
  		{set;_last_RS{get;~rsN}_mentioned;{time}}
		{//; should no longer be required: {set;_lastRS{get;~rsN}Ping;{time;X}} }
  		{commit}
	}
  	{push;inmsg;<:YES:509834204800876554>{get;~rsText} ({get;counterIn}/{get;~maxIn}), {usermention}{get;userWSicons}{get;rsModIcons}{get;rsTier} joined.}

  	{if;{get;counterIn};==;{get;~maxIn};{//; queue full now}
		{if;{get;~autostart};==;true;
{//;         	{push;inmsg;{randchoose;{get;~startmsg}}}}
          	{set;~start;true}
          	{set;~rsPlace;}
          	{if;{userhasrole;{get;_corp-role-main};{get;uid}};
              {if;{argslength};>;1;{set;~rsPlace;{args;1}}}
          	}
			;
           	{push;inmsg;{execcc;pq;{get;~rsN} "Please use **!start {get;~rsN} corp** to indicate where you will run!"}}
        }
		;
{//;      	{push;inmsg;RS{get;~rsN} anyone else?}
      	{execcc;pingRS;{get;~rsN};embedded}
		{execcc;INtimers;{get;~rsN} {get;uid} {get;~thisIn} {usermention}{get;userWSicons} PASS}
}
	}
}
{if;{get;counterIn};<;{get;~maxIn};
	{execcc;INtimers;{get;~rsN} {get;uid} {get;~thisIn} {usermention}{get;userWSicons} PASS}
  	;
{//;  	{void;{send;482308841975971840;not setting {get;~rsN} timers for {usernick;{get;uid}} ( {get;counterIn}/{get;~maxIn})}}}
}
{void;{send;{channelid};{join;{get;inmsg};{newline}}}}
{if;{get;~start};==;false;{execcc;q;{get;~rsN}};{execcc;start;{get;~rsN} {get;~rsPlace}}}

{//; end of lock;write;in (removed temporarily)}
{set;_rs{get;~rsN}lock;false}