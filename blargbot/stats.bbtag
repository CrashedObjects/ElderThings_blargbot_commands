{//; changelog
2018-08-21: - cc created
2018-08-24: - added percentage of RS level
2018-09-04: - added last 10 recent recruits list
2018-10-20: - embed
2018-11-28: - wsinit for RS distribution (POH added)
2018-11-30: moved to guild corp vars
}
{execcc;wsinit;g}
{delete}
{set;~bsoDist;{trim;{execcc;membership}}}
{//;RS distribution}
{void;
  {set;~rsA;[10,9,8,7,6,5]}
  {set;~rsNames;["R10","RS9","RS8","RS7","RS6","RS5"]}
  {set;~usersA;[]}{set;~userTotal;0}{set;~badUsers;[]}
  {void;{foreach;~rs;~rsA;
    {set;~userCount{get;~rs};0}
    {set;~roleid;{roleid;RS{get;~rs};quiet}}
    {if;{get;~roleid};!=;;
      {set;~arrayOfMembers;{rolemembers;{get;~roleid};quiet}}
	  {void;{foreach;~userid;~arrayOfMembers;
        {if;{userhasrole;{get;_corp-role-main};{get;~userid}};
      	  {if;{indexof;{get;~usersA};{get;~userid}};==;-1;
	        {push;~usersA;{get;~userid}}
	        {increment;~userCount{get;~rs}}
	        {increment;~userTotal}
	      }
          ;{if;{indexof;{get;~badUsers};{get;~userid}};==;-1;{push;~badUsers;{get;~userid}}}
        }
      }}
    }
  }}
}
{set;~rsDistTitle;Players based on <:star_red_rs:379292716154093569>RS#}
{set;~rsDistA;[]}
{push;~rsDistA;```java}
{for;~rsInd;0;<;{length;{get;~rsA}};
  	{set;~rs;{get;~rsA;{get;~rsInd}}}
  	{push;~rsDistA;{realpad;{get;~rsNames;{get;~rsInd}};5;{space};right}{realpad;{get;~userCount{get;~rs}};5;{space};left}{space;3}({realpad;{round;{math;*;100;{math;/;{get;~userCount{get;~rs}};{get;~userTotal}}}};2;{space};left}%)}
}
{push;~rsDistA;Total members: {get;~userTotal}```}

{set;~topRS;{trim;{execcc;toprs;all;all;10;1}}}
{//;Last 10 recruits}
{set;~lastReqA;[]}
{if;{length;{get;_recentRecruits}};>;0;
	{set;~len;10}
	{set;~index;{length;{get;_recentRecruits}}}
	{if;{get;~len};>;{get;~index};{set;~len;{get;~index}}}
	{set;~rr;[]}
	{void;{repeat;
      {decrement;~index}
      {push;~rr;{if;{userroles;{get;_recentRecruits;{get;~index}}};==;[];~~{usernick;{get;_recentRecruits;{get;~index}}}~~
        ;
        {usernick;{get;_recentRecruits;{get;~index}}}
      }}
      ;{math;-;{get;~len};0}
  }}
}
{set;~lastReqTitle;Last {get;~len} recruited members}
{set;~lastReq;{join;~rr;,{space}}}
{set;~topTM;{trim;{execcc;toptm;all;10;raw}}}
{void;{send;{channelid};{embedbuild;color:grey;title:Black Star Order statistics;description:{get;~bsoDist}{newline}**{get;~rsDistTitle}**{newline}{join;{get;~rsDistA};{newline}}{newline}{get;~topTM}{newline}{get;~topRS}{newline}**{get;~lastReqTitle}**{newline}{get;~lastReq};footer.text:This list was produced by !{commandname} {join;{argsarray};{space}} on request by {usernick};timestamp:{time;;;{usertimezone}}}}}