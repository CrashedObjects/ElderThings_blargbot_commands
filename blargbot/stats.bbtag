{//; changelog
2018-08-21: - cc created
2018-08-24: - added percentage of RS level
2018-09-04: - added last 10 recent recruits list
2018-10-20: - embed
2018-11-28: - wsinit for RS distribution (POH added)
2018-11-30: - moved to guild corp vars
2019-03-12: - reduced loops, using userMaxRS from !membership if available
			- show if a recent recruit is no longer a member, but not removed from the list yet
}
{execcc;wsinit;g}
{delete}
{set;~bsoDist;{trim;{execcc;membership}}}
{//;RS distribution}
{function;userRS;{//; return the max RS# for a user, less loops}
  {set;~uid;{params;0}}
  {if;0{get;~uid};
    {if;0{get;~maxrs_{get;~uid}};{//; we have it, skip calc}
{get;~maxrs_{get;~uid}}
      ;{//; not present, calculate}
  	  {set;~maxrs_{get;~uid};0}
  	  {set;~rsA;[10,9,8,7,6,5]}
  	  {set;~i;0}{set;~lasti;{math;-;{length;{get;~rsA}};1}}
  	  {void;{while;{logic;&&;{bool;{get;~i};<=;{get;~lasti}};{bool;{get;~maxrs_{get;~uid}};==;0}};
    	{set;~rsrid;{roleid;RS{get;~rsA;{get;~i}};quiet}}
    	{if;{get;~rsrid};!=;;{if;{userhasrole;{get;~rsrid};{get;~uid}};{set;~maxrs_{get;~uid};{get;~rsA;{get;~i}}}}}
    	{increment;~i}
  	  }}
{get;~maxrs_{get;~uid}}
    }
    ;
11
  }
}
{void;
  {set;~rsA;[10,9,8,7,6,5]}
  {foreach;~rs;~rsA;{set;~userCount{get;~rs};0}}
  {set;~rsNames;["R10","RS9","RS8","RS7","RS6","RS5"]}
  {set;~usersA;[]}{set;~userTotal;0}{set;~badUsers;[]}
  {void;{foreach;~corpid;{get;_corp-role-main};
      {foreach;~member;{rolemembers;{get;~corpid};quiet};
       	{set;~mrs;{trim;{func.userRS;{get;~member}}}}
        {if;{get;~mrs};>;0;
	        {if;0{get;~userCount{get;~mrs}};{increment;~userCount{get;~mrs}};{set;~userCount{get;~mrs};1}}
	        {push;~usersA;{get;~member}}
			;
          	{push;~badUsers;{get;~member}}
        }
      }
  }}
}
{set;~rsDistTitle;Players based on <:star_red_rs:379292716154093569>RS#}
{set;~rsDistA;[]}
{push;~rsDistA;```java}
{set;~rsInd;-1}
{set;~userTotal;{length;{get;~usersA}}}
{foreach;~rs;~rsA;
  	{set;~rsName;{get;~rsNames;{increment;~rsInd}}}
  	{if;0{get;~userCount{get;~rs}};;{set;~userCount{get;~rs};0}}
  	{push;~rsDistA;{realpad;{get;~rsName};5;{space};right}{realpad;{get;~userCount{get;~rs}};5;{space};left}{space;3}({realpad;{round;{math;*;100;{math;/;{get;~userCount{get;~rs}};{get;~userTotal}}}};2;{space};left}%)}
}
{push;~rsDistA;Total members: {get;~userTotal}{if;{length;{get;~badUsers}};>;0;{space}({join;~badUsers;,{space}})}```}

{set;~topRS;{trim;{execcc;toprs;all;all;10;1}}}
{//;Last 10 recruits}
{set;~lastReqA;[]}
{if;{length;{get;_recentRecruits}};>;0;
	{set;~len;10}
	{set;~index;{length;{get;_recentRecruits}}}
	{if;{get;~len};>;{get;~index};{set;~len;{get;~index}}}
	{set;~rr;[]}
	{void;{repeat;
      {decrement;~index}
      {push;~rr;{if;{userroles;{get;_recentRecruits;{get;~index}}};==;[];{//; no roles - left server?}
        ~~{usernick;{get;_recentRecruits;{get;~index}}}~~
        ;
        {if;{userhasrole;{get;_corp-role-main};{get;_recentRecruits;{get;~index}}};{usernick;{get;_recentRecruits;{get;~index}}}
          ;{//; no longer a recruit - guestified / retired?}
          (~~{usernick;{get;_recentRecruits;{get;~index}}}~~)
        }
      }}
      ;{math;-;{get;~len};0}
  }}
}
{set;~lastReqTitle;Last {get;~len} recruited members}
{set;~lastReq;{join;~rr;,{space}}}
{set;~topTM;{trim;{execcc;toptm;all;10;raw}}}
{void;{send;{channelid};{embedbuild;color:grey;title:Black Star Order statistics;description:{get;~bsoDist}{newline}**{get;~rsDistTitle}**{newline}{join;{get;~rsDistA};{newline}}{newline}{get;~topTM}{newline}{get;~topRS}{newline}**{get;~lastReqTitle}**{newline}{get;~lastReq};footer.text:This list was produced by !{commandname} {join;{argsarray};{space}} on request by {usernick};timestamp:{time;;;{usertimezone}}}}}