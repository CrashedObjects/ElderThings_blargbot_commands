{//; changelog
2018-11-30: moved to guild corp vars
}
{if;{argslength};
  {switch;{lower;{args;0}};
  status;
    {execcc;wsinit;g}
	{set;~wscmdschan;{get;_ws-chan-comm-main}}
    {set;~boardslist;[]}
    {for;~i;0;<;{length;{get;~wscmdschan}};
      {set;~chan;{get;~wscmdschan;{get;~i}}}
      {set;~msgtime;{time;;{messagetime;{get;~chan};{get;_{get;~chan}_status_msg};X};X}}
      {push;{get;~boardslist}; {if;0{get;~msgtime};{get;~msgtime};{get;_{get;~chan}_status_msg_time}} - {channelname;{get;~chan}}}
    }
    {sort;{get;~boardslist}}
```
{join;{get;~boardslist};{newline}}```
  {return}
  }
}
{if;0{get;_interval_send_count};>;0;{return};{set;_interval_send_count;0}}
{execcc;wsinit;g}
{set;~mainwsTags;{get;_ws-tags-main}}
{set;~maincorpNicks;{get;_corp-name-main}}
{set;~maincorpIDs;{get;_ws-role-main}}
{set;~wscmdschan;{get;_ws-chan-comm-main}}

{set;~now;{time;X}}

{set;~sortedcorps;[]}
{for;~index;0;<;{length;{get;~maincorpIDs}};
  {if;{parseint;{messagetime;{get;~wscmdschan;{get;~index}};{get;_{get;~wscmdschan;{get;~index}}_status_msg};X}};!=;NaN;
    {set;~msgtime;{messagetime;{get;~wscmdschan;{get;~index}};{get;_{get;~wscmdschan;{get;~index}}_status_msg};X}}
  ;
    {if;0{get;_{get;~wscmdschan;{get;~index}}_status_msg_time};
      {set;~msgtime;{time;X;{get;_{get;~wscmdschan;{get;~index}}_status_msg_time}}}
    ;
      {set;~msgtime;{math;-;{get;~now};3601}}
    }
  }
  {push;~sortedcorps;{realpad;{get;~msgtime};10}{realpad;{get;~maincorpIDs;{get;~index}};20}{realpad;{get;~wscmdschan;{get;~index}};20}{get;~mainwsTags;{get;~index}}}
}

{sort;{get;~sortedcorps}}
{set;~statusmsgtimes;[]}
{set;~maincorpIDs;[]}
{set;~wscmdschan;[]}
{set;~mainwsTags;[]}
{foreach;~corp;{get;~sortedcorps};
  {push;~statusmsgtimes;{trim;{substring;{get;~corp};0;10}}}
  {push;~maincorpIDs;{trim;{substring;{get;~corp};10;29}}}
  {push;~wscmdschan;{trim;{substring;{get;~corp};30;49}}}
  {push;~mainwsTags;{trim;{substring;{get;~corp};50}}}
}

{for;~wsindex;0;<;{length;{get;~maincorpIDs}};
  {if;0{get;_interval_send_count};==;0;
    {set;~corpid;{get;~maincorpIDs;{get;~wsindex}}}
    {if;{logic;&&;{bool;{math;-;{get;~now};{get;~statusmsgtimes;{get;~wsindex}}};>;3600};{bool;{math;-;0{get;_{get;~corpid}WSEnd};{get;~now}};>;-3600}};
        {//; If the last board update was over an hour ago AND the WS is underway or has been over for less than an hour.}
      {set;~corpchan;{get;~wscmdschan;{get;~wsindex}}}
      {set;~wstag;{get;~mainwsTags;{get;~wsindex}}}
      {if;0{get;_{get;~corpchan}_status_msg};{delete;{get;~corpchan};{get;_{get;~corpchan}_status_msg}}}
      {set;_{get;~corpchan}_status_msg;{send;{get;~corpchan};{execcc;wstimes;{get;~wstag}}}}
      {if;0{get;_{get;~corpchan}_status_msg};
        {set;_{get;~corpchan}_status_msg_time;{time}}
      }
      {set;_interval_send_count;1}
      {push;{get;_automation_log};{time} - refresh-ws-boards - Refreshed {get;~wstag} wstimes board.}
    ;
      {push;{get;_automation_log};{time} - refresh-ws-boards - {get;~mainwsTags;{get;~wsindex}} board was not due for an update.}
    }
  ;
    {push;{get;_automation_log};{time} - refresh-ws-boards - did not attempt update: No {lb}send{rb} available.}
  }
}