{function;timeDiff;
  {void;
    {set;~time;}
    {if;{paramslength};<;2;;{//; we need 2 timestamps given}
  	{set;~t1;{params;0}}{set;~t2;{params;1}}
    {set;~showZeroDays;0}{if;{paramslength};>;2;{set;~showZeroDays;{params;2}}}
  	{set;~sec;{abs;{math;-;{get;~t2};{get;~t1}}}}
	{set;~days;{floor;{math;/;{get;~sec};86400}}}
		{//;Parse Remaining Hours in Days:}
	{set;~hours;{floor;{math;/;{math;%;{get;~sec};86400};3600}}}
		{//;Parse Time to Hours:}
	{set;~hours2;{floor;{math;/;{get;~sec};3600}}}
		{//;Parse Time to Minutes:}
	{set;~minutes;{floor;{math;/;{math;%;{get;~sec};3600};60}}}
 {//;       {set;~wsT;{get;~wsd}d {get;~wsdh}h {get;~wsm}m}	}
    {set;~time;}
  	{if;{get;~days};>;0;
      	{set;~time;{get;~days}d{space}}
      	;
      	{if;{get;~showZeroDays};>;0;{set;~time;{get;~days}d{space}}}{//;{set;~time;{space;3}}}
  	}
	{set;~time;{get;~time}{realpad;{get;~hours};2;0;left}:{realpad;{get;~minutes};2;0;left}}
    }
  }{get;~time}  
}

{set;~eventOwner;383719299421962242}
{set;~eventStart;{time;X;2018-11-23 01:31;YYYY-MM-DD HH:mm}}
{set;~betBefore;{time;X;2018-11-28 23:59;YYYY-MM-DD HH:mm}}
{set;~eventDone;}{//;{time;X;2019-12-31 23:59;YYYY-MM-DD HH:mm}}
{set;~winner;}
{set;~now;{time;X}}
{set;~msg;[]}
{if;{userid};==;{get;~eventOwner};
  	{if;{logic;&&;{bool;{get;~eventDone};!=;};{bool;{get;~now};>;{get;~eventDone}}};
      	{push;~msg;Congrats, All-mighty Effrafax! You reached 4m credit cap in {func.timeDiff;{get;~eventDone};{get;~eventStart};0}}
		;{push;~msg;Hello, Eff! You have these folks watching your every step to claim the reward. Godspeed!}
    }
   	;{//; not effrafax}
  	{if;{logic;&&;{bool;{get;~eventDone};!=;};{bool;{get;~now};>;{get;~eventDone}}};
      {push;~msg;There was this day Effrafax decided to undertake the steep climb to 4m credit cap. He offered a reward of 1 RS9 art to whomever made the closest guess when he would manage to reach the goal. He got his spreadsheets out, put em on a big table and drew, scratched, deleted and started over again. Until it was done. His prediction was accurate to the sixth decimal point of the millisecond, by the way. The reward went to {get;~winner}. Well done, both!{newline}See below how far everybody was.}{set;~now;{get;~eventDone}}
      ;{//; event underway}
      {push;~msg;Welcome to Effrafax's puzzle, started on {time;D MMM YYYY HH:mm;{get;~eventStart};X}:{newline}"**I have gone through all of my nerd math several times and now have an accurate date for when I will hit 4mil and RS9. Do people want to ante an RS7 or RS8 and bet on when I'll hit $4mil? I'll throw in a shiny RS9 art to the winner?**{newline}Oh, and for informational purposes, my cap is currently $3.34mil and I have 15 planets."}
      {if;{get;~now};>;{get;~betBefore};
        {push;~msg;Bets are closed!}
     	;{//; bets open}
	  	{if;{argslength};>;0;
  		  {if;0{get;_bets_eff_{userid}};
          	{push;~msg;You have already set your bet in stone. Cannot change!}
      		;{//; bet allowed, check if legit}
      		{set;~time;{trim;{execcc;parsetime;{join;{argsarray};{space}}}}}
      		{if;0{get;~time};<;600;Bad input (or time is less than 10 minutes), try again! See `!help afk` for proper time format.{return}
          		;{//; legit bet - record it}
          		{set;_bets_eff_{userid};{math;+;{get;~now};{get;~time}}}
          		{push;~msg;Thank you for betting!}
        	}
          }
          ;
          {push;~msg;Bets are open till __{time;D MMM YYYY HH:mm;{get;~betBefore};X}__ GMT.}
    	}
      }
    }
}
{set;~bets;[]}  	
{foreach;~uid;{guildmembers};
    {if;0{get;_bets_eff_{get;~uid}};
        {if;{get;_bets_eff_{get;~uid}};<;{get;~now};
            {push;~bets;{math;-;{get;_bets_eff_{get;~uid}};{get;~now}}!{realpad;{usernick;{get;~uid}};20;{space};right}: {realpad;{trim;{func.timeDiff;{get;~now};{get;_bets_eff_{get;~uid}};0}};10;{space};left} AGO!}
            ;
        	{push;~bets;{math;-;{get;_bets_eff_{get;~uid}};{get;~now}}!{realpad;{usernick;{get;~uid}};20;{space};right}: {realpad;{trim;{func.timeDiff;{get;_bets_eff_{get;~uid}};{get;~now};0}};10;{space};left}}
        }
   	}
}
{sort;~bets}
{join;{get;~msg};{newline}}
{if;{length;{get;~bets}};==;0;Oh, no, noone has been brave enough to place a bet, yet.;
**Bets for Eff's puzzle**
```{foreach;~line;~bets;{set;~arr;{split;{get;~line};!}}{get;~arr;1}{newline}}```
}