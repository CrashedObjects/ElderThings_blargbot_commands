{function;usage;__**usage:**__ !{commandname} [ add | del | list | undo ]

Manipulates a list of blargbot guild variables.
}

{function;createvar;{if;{length;{get;_guild_vars}};==;0;{set;_guild_vars;[]}{set;_guild_vars_desc;[]}}
}

{switch;{lower;{args;0}};
add;
	{func.createvar}
	{push;_guild_vars;{args;1}}
  	{if;{argslength};>;2;{push;_guild_vars_desc;{args;2;n}};{push;_guild_vars_desc;{zws}}}
Added {args;1} to list. Use `!{commandname} undo` to undo this action.
	{set;_guild_vars_last_action;add}
	{set;_guild_vars_last_item;{args;1}}
  	{set;_guild_vars_desc_last_item;{args;2;n}}
;
["del","remove"];
	{func.createvar}
	{set;~foundentry;{indexof;{lower;{get;_guild_vars}};{lower;{args;1}}}}
	{if;{get;~foundentry};>;-1;{set;_guild_vars_last_item;{splice;{get;_guild_vars};{get;~foundentry};1}}{set;_guild_vars_last_item;{get;_guild_vars_last_item;0}}{set;_guild_vars_desc_last_item;{splice;{get;_guild_vars_desc};{get;~foundentry};1}}{set;_guild_vars_desc_last_item;{get;_guild_vars_desc_last_item;0}}Deleted `{lower;{args;1}}` from the list. Use `{commandname} undo` to undo this action.{set;_guild_vars_last_action;del}
     ;
    	Couldn't find `{args;1}` in the list.
     }
;
list;
	{func.createvar}
	{repeat;`;3}java{newline}Guild Variables list{newline}--------------------{newline}{for;~index;0;<;{length;{get;_guild_vars}};{get;_guild_vars;{get;~index}} - {get;_guild_vars_desc;{get;~index}}{newline}}{repeat;`;3}
;
clear;
	{set;_guild_vars_last_action;clear}
	{set;_guild_vars_last_item;{get;_guild_vars}}
  	{set;_guild_vars_desc_last_item;{get;_guild_vars_desc}}
	{set;_guild_vars;}
  	{set;_guild_vars_desc;}
Cleared the list. Use `{commandname} undo` to undo this action.
;
undo;
	{switch;{get;_guild_vars_last_action};
	add;
Undid addition of `{pop;{get;_guild_vars}}` to the list.
{void;{pop;{get;_guild_vars_desc}}}
    {set;_guild_vars_last_action;undo}
	;
	del;
	{push;{get;_guild_vars};{get;_guild_vars_last_item}}
    {push;{get;_guild_vars_desc};{get;_guild_vars_desc_last_item}}
Undid removal of `{get;_guild_vars_last_item}` from the list.
	{set;_guild_vars_last_action;undo}
	;
	clear;
	{set;_guild_vars;{get;_guild_vars_last_item}}
    {set;_guild_vars_desc;{get;_guild_vars_desc_last_item}}
Restored list.
	{set;_guild_vars_last_action;undo}
	;
	undo;
	Last command was an undo operation. Can't undo an undo!
	;
	No last action?
	;
	}
;
{func.usage}
}