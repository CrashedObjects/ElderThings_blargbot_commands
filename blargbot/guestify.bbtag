{//; changelog
2018-08-31: fix to not target self if it can't find the user you're talking about
2018-09-25: do not remove talkativeness rank roles
}
{set;guestrole;369615227454488576}
{set;logchan;411220744702394369}

{//;check there is only a single argument}
{if;{argslength};!=;1;
    __**Command Name**__: {commandname}
    __**Usage**__: !guest <user>
    {return}
}

{set;~userid;{userid;{args;0};quiet}}
{if;{length;{get;~userid}};==;0;{//; check if username is valid}
    {args;0} doesn't appear to be a valid username.
    {return}
}
{set;~user;{username;{get;~userid};quiet}}
{set;~usernick;{usernick;{get;~userid}}}
{set;~rolesList;{get;_activityRolesList}}{//; as coped from !roles}

{set;~actRoles;[]}
{void;{foreach;~roleStr;{split;{get;~rolesList};{newline}};
	{set;~roleArr;{split;{get;~roleStr};{space}}}
    {set;~roleName;{trim;{get;~roleArr;1}}}
    {set;~roleid;{get;~roleArr;0}}
  	{push;~actRoles;{get;~roleid}}
}}
{//;remove all roles}
{set;~roles;{roles;{get;~userid}}}
{set;~index;-1}
{set;~notRemoved;[]}
{set;~nElements;{length;{get;~roles}}}
{void;{repeat;
    {increment;~index}
	{if;{indexof;{get;~actRoles};{get;~roles;{get;~index}}};==;-1;{//; only remove role if not in the list}
       {removerole;{get;~roles;{get;~index}};{get;~userid}}
      ;{push;~notRemoved;{rolename;{get;~roles;{get;~index}}}}
    }
  	;
    {get;~nElements}
}}

☑ Removed all roles {if;{length;{get;~notRemoved}};>;0;(except {join;{get;~notRemoved};, })} from `​{get;~user}`​.
{//;add guest role}
{if;{addrole;{get;guestrole};{get;~userid}};☑ Added guest role to `​{get;~user}`​.;❌ Unable to add guest role to `​{get;~user}`​. Did they already have it?}
{//;prepend [] to nickname}
{if;{setnick;[] {username;{get;~userid}};{get;~userid}};includes;Could not change nickname;❌ Could not revert `​{get;~user}`​'s nickname. Permissions issue?;  ☑ Renamed `​{get;~usernick}`​ to `​[] {get;~user}`​.
}

Do they need to be banned from becoming an ally in the future? Type `!blacklist add {get;~user}`.
{//;We changed roles. Update this array for the !farewell command}
{set;_{get;~userid}_last_roles;{userroles;{get;~userid}}}

{void;{send;{get;logchan};`​{usernick;{userid}}`​ reverted `​{get;~user}`​ to guest status.}}
{set;_{get;~userid}_usernick;{usernick;{get;~userid}}} {//; me may have changed nickname. Set guild variable accordingly}