{//; backup working version, just in case.}
{//;changelog 2018-08-04
- added Opponent
- added preparation time
2018-08-05: added SCANNING status
2018-08-12: added local times for all memebers when called with a valid ws-tag or from a -ws channel
2018-09-18: -jordan. Added recleague-ws and forbid member times being displayed in public channels
2018-09-19: added public restriction if called from opponent channels or from allies
2018-09-? : added last-seen column to ws-member's local times 
2018-10-05: reworked last-seen to discord activity bar, added RS activity graph-bar (only in leaders channels)
2018-10-13: added AFK info
2018-10-22: -jordan. added multiple time formats in func.humanTimeDiff.
to-do: delete the local times list after a timeout to keep the ws-channel tidy
}
{//;shows WS status for all BSO corps}
{//;
{if;{userid};!=;383227005589782528;The command is being worked on, please be patient!{return}}
}
{switch;{commandname};
wstimes;
{timer;{delete};3s}
;
}
{set;~mainwsTags;["BSO-WS","FIN-WS","ENF-WS","DAU-WS","OBN-WS"]}
{set;~altwsTags;["gmu-ws","prophecy-ws","recleague-ws"]}
{set;~maincorpNicks;Black;Final;Enforcers;Dauntless;Obsidian}
{set;~altcorpNicks;G.M.U.;Prophecy;RecLeague}
{set;~maincorpIDs;387715016222048265;387715242957864971;387715522575204352;401489230951481354;406431073296318465}
{set;~altcorpIDs;472262667789139988;472262704984489984;490946875768176641}
{set;~wsChannels;["384350754363670528","382541038973222915","382541094438699009","415943986147098624"]}
{set;~wsLeadCh;["387620392186937345","388121039546351618","394903358215028757","492932807031783425"]}{//;leaders only}
{set;~vsChannels;["479306306981724170","410887759893626881","423596924768354304","423595847679475732"]}{//; consider public}
{set;~everyoneCategory;372174329456230403}
{set;~allyrole;455134557532848139}
{set;~chId;{channelid}}{set;~chCat;{channelcategory}}{set;~userid;{userid}}
{//;
{set;~chCat;384331980852166656}{set;~chId;382541038973222915}{//;simulate call from enf-ws}
{set;~chCat;379606694508101642}{set;~chId;368396489455697920}{//;simulate call from members}
{set;~chCat;372174329456230403}{set;~chId;368229482579886094}{//;simulate call from enf-opponent}
{if;{userid};==;383227005589782528;{set;~userid;390220162906718208}}{//; simulate an ally}
}
{if;{get;~chCat};==;{get;~everyoneCategory};{set;~public;1};{set;~public;}}
{if;{indexof;{get;~vsChannels};{get;~chId}};!=;-1;{set;~public;1}}{//; treat -vs-opponent channels as public too}
{if;{userhasrole;{get;~allyrole};{get;~userid}};{set;~public;1}}{//; treat allies as public too}
{//;
{set;~dbgchannel;482308841975971840}{if;{channelid};==;{get;~dbgchannel};{set;~public;1};{set;~public;}}{//;override}
}
{switch;{commandname};
wstimes;
;
  {set;~public;0}
 {//; Ignore the public check if called from autoresponse (or !afk or !back)}
}

{if;0{get;~public};
  {set;~wsTags;{get;~mainwsTags}}
  {set;~corpNicks;{get;~maincorpNicks}}
  {set;~corpIDs;{get;~maincorpIDs}}
;
  {set;~wsTags;{concat;{get;~mainwsTags};{get;~altwsTags}}}
  {set;~corpNicks;{concat;{get;~maincorpNicks};{get;~altcorpNicks}}}
  {set;~corpIDs;{concat;{get;~maincorpIDs};{get;~altcorpIDs}}}
}
{//;
public={get;~public}, chID={get;~chId}, chCat={get;~chCat}
}
{//; if called with ws!=-tag or called from -ws channel - give local time for members}
{function;timeDiff;
  {void;
    {set;~time;}
    {if;{paramslength};<;2;;{//; we need 2 timestamps given}
  	{set;~t1;{params;0}}{set;~t2;{params;1}}
    {set;~showZeroDays;0}{if;{paramslength};>;2;{set;~showZeroDays;{params;2}}}
  	{set;~sec;{abs;{math;-;{get;~t2};{get;~t1}}}}
	{set;~days;{floor;{math;/;{get;~sec};86400}}}
		{//;Parse Remaining Hours in Days:}
	{set;~hours;{floor;{math;/;{math;%;{get;~sec};86400};3600}}}
		{//;Parse Time to Hours:}
	{set;~hours2;{floor;{math;/;{get;~sec};3600}}}
		{//;Parse Time to Minutes:}
	{set;~minutes;{floor;{math;/;{math;%;{get;~sec};3600};60}}}
 {//;       {set;~wsT;{get;~wsd}d {get;~wsdh}h {get;~wsm}m}	}
    {set;~time;}
  	{if;{get;~days};>;0;
      	{set;~time;{get;~days}d{space}}
      	;
      	{if;{get;~showZeroDays};>;0;{set;~time;{get;~days}d{space}}}{//;{set;~time;{space;3}}}
  	}
	{set;~time;{get;~time}{realpad;{get;~hours};2;0;left}:{realpad;{get;~minutes};2;0;left}}
    }
  }{get;~time}  
}
{function;humanTimeDiff;
  {set;~result}
  {if;{paramslength};>=;2;{//; we need 2 timestamps given}
    {set;~diff;{abs;{math;-;{params;0};{params;1}}}}
    {set;~d;{rounddown;{math;/;{get;~diff};86400}}}
    {set;~d_remain;{math;%;{get;~diff};86400}}
    {set;~h;{rounddown;{math;/;{get;~d_remain};3600}}}
    {set;~h_remain;{math;%;{get;~d_remain};3600}}
    {set;~m;{rounddown;{math;/;{get;~h_remain};60}}}
    {set;~m_remain;{math;%;{get;~h_remain};60}}
    {switch;{params;2};
      short; {//; displays 1 time division tersely, no rounding}
        {if;{get;~d};{get;~d}d;{if;{get;~h};{get;~h}h;{if;{get;~m};{get;~m}m;{get;~m_remain}s}}}
      ;
      twodivs; {//; displays up to 2 time divisions, no rounding}
        {if;{get;~d};
          {get;~d} day{if;{get;~d};!=;1;s}{if;{get;~h};{space}{get;~h} hour{if;{get;~h};!=;1;s}}
        ;
          {if;{get;~h};
            {get;~h} hour{if;{get;~h};!=;1;s}{if;{get;~m};{space}{get;~m} minute{if;{get;~m};!=;1;s}}
          ;
            {if;{get;~m};
              {get;~m} minute{if;{get;~m};!=;1;s}{if;{get;~m_remain};{space}{get;~m_remain} second{if;{get;~m_remain};!=;1;s}}
            ;
              {get;~m_remain} second{if;{get;~m_remain};!=;1;s}
            }
          }
        }
      ;
      twodivsshort; {//; displays 2 time divisions tersely, no rounding}
         {if;{get;~d};{get;~d}d{if;{get;~h};{space}{get;~h}h};{if;{get;~h};{get;~h}h{if;{get;~m};{space}{get;~m}m};{if;{get;~m};{get;~m}m{if;{get;~m_remain};{space}{get;~m_remain}s};{get;~m_remain}s}}}
      ;
      round; {//; displays 1 time division, rounded up or down based on next division}
        {if;{get;~d};
          {if;{get;~h};>=;12;{void;{increment;~d}}}
          {get;~d} day{if;{get;~d};!=;1;s}
        ;
          {if;{get;~h};
            {if;{get;~m};>=;30;{void;{increment;~h}}}
            {get;~h} hour{if;{get;~h};!=;1;s}
          ;
            {if;{get;~m};
              {if;{get;~s};>=;30;{void;{increment;~m}}}
              {get;~m} minute{if;{get;~m};!=;1;s}
            ;
              {get;~m_remain} second{if;{get;~m_remain};!=;1;s}
            }
          }
        }
      ; {//; displays 1 time division, no rounding. Default if no format specified}
        {if;{get;~d};{get;~d} day{if;{get;~d};!=;1;s};{if;{get;~h};{get;~h} hour{if;{get;~h};!=;1;s};{if;{get;~m};{get;~m} minute{if;{get;~m};!=;1;s};{get;~m_remain} second{if;{get;~m_remain};!=;1;s}}}}
    }
  }
}
  
{set;~wsTagIndex;-1}{//;default value - show WS status for main corps}
{if;{argslength};>;0;
  	{set;~indByArg;{indexof;{get;~wsTags};{upper;{args;0}}}}
  	{if;{get;~indByArg};!=;-1;{set;~wsTagIndex;{get;~indByArg}};{set;~wsTagIndex;-2}}
}
{//;  channelid={channelid}, wsChannels={join;{get;~wsChannels};,}, wsTagInd={get;~wsTagIndex}}
{switch;{get;~wsTagIndex};
-2;{//; arg incorrect}
{set;~error;`{args}` is not a valid WS corp. Please specify as discord role.{newline}}
;  
-1;{//; arg not given - get the corp index from the corp channel}
  {set;~indByChannel;{indexof;{get;~wsChannels};{get;~chId}}}
  {if;{get;~indByChannel};==;-1;{set;~indByChannel;{indexof;{get;~wsLeadCh};{get;~chId}}}}
  {if;{get;~indByChannel};!=;-1;{//; called from valid channel}
    {set;~wsTagIndex;{get;~indByChannel}}
  	{set;~corpIDs;["{get;~corpIDs;{get;~wsTagIndex}}"]}
  	{set;~corpNicks;["{get;~corpNicks;{get;~wsTagIndex}}"]}
  }
;
{//; correct ws-tag given - return info for this corp only + times for all members}
  {set;~corpIDs;["{get;~corpIDs;{get;~wsTagIndex}}"]}
  {set;~corpNicks;["{get;~corpNicks;{get;~wsTagIndex}}"]}
}
{set;~corpWS;[]}
{set;~res}
{set;~index;-1}
{void;{repeat;
  {increment;~index}
  {set;~roleid;{get;~corpIDs;{get;~index}}}
  {if;{length;{get;_{get;~roleid}WSScan}};>;0;{set;~wsScan;{get;_{get;~roleid}WSScan}};{set;~wsScan;-1}}
  {if;{length;{get;_{get;~roleid}WSEnd}};>;0;{set;~wsEnd;{get;_{get;~roleid}WSEnd}};{set;~wsEnd;-1}}
  {if;{logic;||;{bool;{get;~wsEnd};>;0};{bool;{get;~wsScan};>;0}};
	{if;{get;~wsScan};>;0;
      {set;~wsStatus;SCANNING{get;_{get;~roleid}WSScanTier}{space}/{space}for{space}{trim;{func.timeDiff;{get;~wsScan};{time;X};0}}}
      ; {//; not scanning}
      {set;~wsSecondsLeft;{math;-;{get;~wsEnd};{time;X}}}
      {if;{get;~wsSecondsLeft};<;0;
        {set;~wsStatus;Ended}	      	{//;if time is up, set status to Ended}
      	;{//; not ended}
      	{if;{get;~wsSecondsLeft};>;388800;  {//;if still in preparation period}
        	{set;~wsPrep;Starts in{space}} {//;show prep time remaining}
        	{set;~wsEnd;{math;-;{get;~wsEnd};388800}}
        	;
        	{set;~wsPrep;}
      	}
        {if;{length;{get;~wsPrep}};!=;0;
          {set;~wsTime;{func.timeDiff;{get;~wsEnd};{time;X};0}}
          ;
          {set;~wsTime;{realpad;{func.timeDiff;{get;~wsEnd};{time;X};0};8;{space};left}}
        }
{//;        {set;~wsTime;{func.timeDiff;{get;~wsEnd};{time;X};{if;{length;{get;~wsPrep}};!=;0;0;1}}}}
      	{//;Check WS opponent, if set add it to the status:}
		{set;~wsOpp;{get;_{get;~roleid}WSOpp}}
  		{if;{length;{get;~wsOpp}};>;0;{set;~wsOpponent;{space}v {get;~wsOpp}};{set;~wsOpponent;}}
        
        {set;~wsStatus;{get;~wsPrep}{get;~wsTime}{get;~wsOpponent}}
      }
    }
    ; {//; WSEnd and WSScan not set}
    {set;~wsStatus;---}{//;WS-tag not found}
  }
  {push;~corpWS;{get;~wsStatus}}
  {if;{length;{get;~res}};>;0;{set;~res;{get;~res}{newline}}}
  {set;~res;{get;~res}{realpad;{get;~corpNicks;{get;~index}};9;{space};left}: {get;~wsStatus;}}
  ;
  {length;{get;~corpIDs}}
}}
{function;cleanNick;{//;userid;[maxlen]}
  	{set;~basenick;{usernick;{params;0}}}
	{void;{while;{get;~basenick};includes;[; {//;remove all bracketed parts}
  		{set;~openbracket;{indexof;{get;~basenick};[}}
  		{set;~closebracket;{indexof;{get;~basenick};]}}
  		{set;~base1;{substring;{get;~basenick};0;{math;-;{get;~openbracket};1}}}
    	{set;~base2;{substring;{get;~basenick};{math;+;{get;~closebracket};1}}}
    	{set;~basenick;{trim;{get;~base1}{get;~base2}}}
	}}
  	{set;~basenick;{regexreplace;{get;~basenick};/[^\x00-\x7F]/g;_}}
{if;{paramslength};>;1;{substring;{get;~basenick};0;{params;1}};{get;~basenick}}
}
{set;~localTimes;[]}

{set;~adminrole;372154093910622220}{if;{userhasrole;{get;~adminrole};{userid}};==;true;{set;~isAdmin;1};{set;~isAdmin;0}}
{if;{indexof;{get;~wsLeadCh};{channelid}};==;-1;{set;~isLead;0};{set;~isLead;1}}
{//;	debug
{if;{get;~isAdmin};{set;~isLead;1}}
}
{set;~isAdmin;0}
{set;~hoursD;6}{set;~lenD;9}
{set;~head1A;["Member{space;9}","Local","|{space;2}Discord{space}"]}
{set;~head2A;["{space;6}{space;9}","{space}Time","|{space}Last seen"]}{//;"Past {get;~hoursD}h"]}
{if;{get;~isLead};{push;~head1A;|{space}RS activity}{push;~head2A;|{space}Next 6h}}
{if;{get;~isAdmin};{push;~head1A;iamactive}{push;~head2A;{space;9}};{push;~head1A;|{space}AFK}{push;~head2A;|{space}{space;9}}}
{void;{if;{get;~wsTagIndex};>;-1;
  	{set;~role;{get;~corpIDs;0}}
	{set;~arrayOfMembers;{rolemembers;{get;~role};quiet}}
  	{set;~uidA;[]}
  	{void;{foreach;~userid;{get;~arrayOfMembers};{//; pre-sort by discord activity}
      {set;~seen;{get;_{get;~userid}_last_post}}
      {if;0{get;~seen};{set;~agoS;{math;-;{time;X};{time;X;{get;~seen}}}};{set;~agoS;9999999999}}
      {push;~uidA;{realpad;{get;~agoS};10;0;left}:{get;~userid}}
    }{sort;~uidA}}
  	{void;{foreach;~uidL;{get;~uidA};
      	{set;~userA;{split;{get;~uidL};:}}{set;~userid;{get;~userA;1}}
      	{set;~userNick;{trim;{func.cleanNick;{get;~userid};15}}}
  		{set;~userTime;{time;HH:mm;;;{usertimezone;{get;~userid}}}}
        {set;~seen;{get;_{get;~userid}_last_post}}
{//;bar for last seen, nobody likes it
      {if;0{get;~seen};
          {set;~agoS;{math;-;{time;X};{time;X;{get;~seen}}}}
          {set;~agoH;{round;{math;/;{get;~agoS};3600}}}
          {if;{get;~agoS};<;180;{set;~ago;{space;{math;-;{get;~lenD};1}}█};{//; less than 30 min}
            {if;{get;~agoH};<;1;{set;~ago;{space;{math;-;{get;~lenD};1}}■};{//; less than 1h}
              {if;{get;~agoH};>;{get;~hoursD};{set;~ago;{realpad;+{get;~agoH};3;{space};left}{space}};{//; more than 6h}
            	{set;~ago;{space;{math;-;{get;~lenD};{get;~agoH}}}■}{//; 1-6h}
          	  }
          	}
          }
          ;{set;~ago;{repeat;-;{get;~lenD}}}
  		}
      	{set;~ago;{space}{realpad;{get;~ago};{get;~lenD};□;right}}
}      
		{//;jordan to make ~ago human-readable}
      	{if;0{get;~seen};{set;~ago;{space}|{space}{realpad;{trim;{func.humanTimeDiff;{time;X};{time;X;{get;~seen}};short} ago};9;{space};left}};{set;~ago;{space;9}}}
      	{set;~localTime;{space}{if;{usertimezone;{get;~userid}};==;UTC;(TZ?);{get;~userTime}}}
      	{set;~active;{space}|{space}}{set;~rsa;}{set;~afk;{space}|{space}}
      	{if;0{get;_{get;~userid}_afk};
            {set;~afkX;{time;X;{get;_{get;~userid}_afk}}}
            {if;{get;~afkX};>;{time;X};
              {set;~active;{space}|{space}due in {trim;{func.humanTimeDiff;{get;~afkX};{time;X};round}}}
              ;{//; late to !back}
              {set;~active;{space}|{space}due {trim;{func.humanTimeDiff;{time;X};{get;~afkX};round}} ago}
            }
        }
      	{if;{get;~isAdmin};{//; not finished, to show next online time based on sleeping habits}
          {if;{get;~active};==;{space}|{space};
          	{set;~active;{space}{realpad;{trim;{execcc;useractive;{get;~userid};GetOutAHere}};16;{space}}}
          }
      	}
      	{if;{get;~isLead};{set;~rsa;{space}|{space}{realpad;{trim;{execcc;rsfunc;RSAgraph;{get;~userid};6}};11;{space};right}}}
	    {if;{length;{get;~userTime}};>;0;
	      {push;~localTimes;{realpad;{get;~userNick};15;{space};right}{get;~localTime}{get;~ago}{get;~rsa}{get;~active}}
    	}
	}}
}}
{if;{length;{get;~error}};==;0;
**WHITE STAR TIME REMAINING**
```java{newline}{get;~res}```
{if;{length;{get;~localTimes}};>;0;
  {if;0{get;~public};Detailed member information is not available in public channels.{newline};
  **Local times for {get;~wsTags;{get;~wsTagIndex}} members**```java{newline}{join;{get;~head1A};{space}}{newline}{join;{get;~head2A};{space}}{newline}{join;{get;~localTimes};{newline}}```}};{get;~error}}{if;0{get;~public}; *Additional information is available when this command is run from a members-only channel.*}