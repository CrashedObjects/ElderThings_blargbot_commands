{//;Changelog
2018-12-10 - Should have been keeping a changelog this whole time. Oops.
2018-12-10 - Adjusted so bot DM warning for !afk in a pub chan even if notify=none
2019-01-14 - updated to use parsetime instead of doing it itself.
2019-01-18 - remove user from RS queus in #bso-rs. Only if _rs_auto_out=true
2019-01-29 - Allow for optional reason to be set for being AFK
2019-11-10 - Adapted for Elder Things

{prefix}cc sethelp afk __**Usage:**__ {prefix}afk <duration> [ reason ]

Sets your status as being away for a length of time. Durations may be specified in days, hours, minutes, seconds.
If a reason is specified, it will be shown in the !seen command.

Use `{prefix}back` to indicate you have returned. 

To customize your AFK settings, type `{prefix}help afkset`

Examples:
{prefix}afk 7 hours
{prefix}afk 2h
{prefix}afk 30 mins
{prefix}afk 2h30m
}

{suppresslookup} {//; suppresses errors from lookup system}
{timer;{delete;{messageid}};5s}
{set;~userid;{userid}}
{execcc;wschanonly;1}

  
{set;~afkchannel;{channelid}}
{if;{userhasrole;{roleid;wsa-everyone}};{set;~afkchannel;{channelid;wsa-afk}}}
{if;{userhasrole;{roleid;wsb-everyone}};{set;~afkchannel;{channelid;wsb-afk}}}

{if;{argslength};<;1;
  __**Command Name:**__ {commandname}
  __**Usage:**__ {prefix}{commandname} <duration> [ reason ]

To indicate you have returned from AFK, type {prefix}back. For more information, type `{prefix}afk help`{return}
}

{if;{args;0};==;help;
  __**Usage:**__ {prefix}afk <duration> [ reason ]

  Sets your status as being away for a length of time. Durations may be specified in days, hours, minutes, seconds.
  If a reason is specified, it will be shown in the !seen command.

  Use `{prefix}back` to indicate you have returned. 

  Examples:
  {prefix}afk 7 hours
  {prefix}afk 2h
  {prefix}afk 30 mins
  {prefix}afk 2h30m
  {return}
}

{if;{logic;&&;{bool;{argslength};>;1};{bool;{userid;{args;0};1};!=;{userid;zzzabcnobodyzzz;1}}};
  {if;{userhasrole;{roleid;officer}};
    {set;~userid;{userid;{args;0}}}
  ;
    {execcc;bgdelmsg;300;{channelid};{send;{channelid};Only officer can set other user's afk status}}
    {return}
  }
}

{if;{get;~userid};==;{userid};
  {set;~parsedtime;{execcc;parsetime;{args}}}
;
  {set;~parsedtime;{execcc;parsetime;{args;1;n};set by {usernick}}}
}

{if;{isarray;{get;~parsedtime}};
  {set;~duration;{parseint;{get;~parsedtime;0}}}
  {if;{length;{get;~parsedtime;1}};>;500;
    {set;~message;{substring;{get;~parsedtime;1};0;500}...}
    {set;~longreason;1}
  ;
    {set;~message;{get;~parsedtime;1}}
  }
;
  {set;~duration;{parseint;{get;~parsedtime}}}
}

{if;{get;~duration};==;0;{execcc;bgdelmsg;300;{channelid};{send;{channelid};Unable to parse your duration properly. Use `{prefix}{commandname} help` for usage information.}}{return}}

{if;{get;~duration};>;172800;
  {execcc;bgdelmsg;300;{channelid};{send;{channelid};Unable to set AFK duration beyond 48h.{newline}}}
  {return}
}

{set;~futureunix;{math;+;{time;X};{get;~duration}}}
{set;~futureutc;{time;;{get;~futureunix};X}}
{//; The following is a workaround for time issue reported at cat town: https://discordapp.com/channels/194232473931087872/238131984017260546/468233337257852932}
{set;~tzparts;{split;{time;Z;;;{usertimezone;{get;~userid}}};:}}
{set;~tzdiff;{math;*;{math;+;{get;~tzparts;0};{math;/;{get;~tzparts;1};60}};3600}}
{set;~futurelocal;{time;;{math;+;{get;~futureunix};{get;~tzdiff}};X}}
{set;~time_in_words;{if;0{get;~days};{space}{get;~days}d}{if;0{get;~hours};{space}{get;~hours}h}{if;0{get;~mins};{space}{get;~mins}m}{if;0{get;~secs};{space}{get;~secs}s}}

{set;~afkresponse;{usermention;{get;~userid}}, you are marked as AFK for the next{get;~time_in_words}. {if;{length;{get;~message}};Reason: {get;~message}}{newline}{if;0{get;~longreason};{newline}{newline}Your AFK reason exceeded 500 characters and was truncated.}{if;0{get;_{get;~userid}_afk};{newline}{newline}You previously marked yourself AFK but didn't use `{prefix}back` to indicate your return. Your AFK time has been updated.}}

{delete}

{//;delete last afk message}
{execcc;dellastafkmsg;{get;~userid}}

{//;save channel and message id before sending message}
{set;_{get;~userid}_lastafkchanid;{get;~afkchannel}}
{set;_{get;~userid}_lastafkmsgid;{send;{get;_{get;~userid}_lastafkchanid};{get;~afkresponse}}}

{set;_{get;~userid}_afk;{get;~futureutc}}
{set;_{get;~userid}_afk_set;{time}}
{if;{isarray;{get;_afk_list}};;{set;_afk_list;[]}}
{if;{indexof;{get;_afk_list};{get;~userid}};==;-1;{push;{get;_afk_list};{get;~userid}}}
{if;{length;{get;~message}};{set;_{get;~userid}_afk_msg;{get;~message}}}

{//;{usernick} is set to AFK for{get;~time_in_words}.{if;{length;{get;_{get;~userid}_afk_msg}};{space}Reason: {get;_{get;~userid}_afk_msg}}}