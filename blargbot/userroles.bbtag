{//; changelog
2019-01-15 - combined !ur and !userroles into one command
           - Made output into an embed
}


{if;{argslength};!=;1;
    __**Command Name**__: {commandname}    
    __**Usage**__: !{commandname} <user>
    {return}
}

{set;~userid;{userid;{args;0}}}

{if;{get;~userid};==;;
    `@{args}` doesn't appear to be a valid username on this Discord server.
    {return}
}

{set;~usernick;{usernick;{get;~userid}}}
{set;~userroleids;{roles;{get;~userid}}}
{set;~numuserroles;{length;{get;~userroleids}}}
{set;~userrolenames;[]}

{void;{foreach;~roleid;{roles;{get;~userid}};
	{set;~time;}  
  	{foreach;~roleRecord;{get;_{get;~userid}_role_history};
      {if;{get;~roleRecord};includes;{get;~roleid}:add:;
      	{set;~arr;{split;{get;~roleRecord};:}}
        {set;~added;{get;~arr;2}}
        {set;~time;{space}(obtained on {time;D MMM YYYY;{get;~added};X}, had it for {trim;{execcc;timediff;two-divs;{time;X};{get;~added}}})}
      }
    }
    {push;~userrolenames;{rolemention;{get;~roleid}} {get;~time}}
}}

{embed;{embedbuild;color:GREY;thumbnail.url:{useravatar;{get;~userid}};fields.name:{get;~usernick} has {get;~numuserroles} role{if;{get;~numuserroles};==;1;;s}.;fields.value:
  {if;{get;~numuserroles};>;0;
    {if;{get;~numuserroles};>;1;
{join;{get;~userrolenames};{newline}}
    ;
      {space;5}It is `{get;~userrolenames;0}`.
    }
  }
}}