{//;Changelog
2019-12-13 - Initial version
}

{suppresslookup} {//; suppresses errors from lookup system}
{set;~afk-wsa-uid;{execcc;listafk;{roleid;WSA-Everyone}}}
{set;~afk-wsb-uid;{execcc;listafk;{roleid;WSB-Everyone}}}
{set;~wsa-afk-chan;{channelid;wsa-afk}}
{set;~wsb-afk-chan;{channelid;wsb-afk}}
{set;~wsa-remind-chan;{channelid;wsa-reminders}}
{set;~wsb-remind-chan;{channelid;wsb-reminders}}
{set;~remindfullaccess;1}
{commit}

{if;{commandname};startswith;_autoresponse_;{set;~forcedupdate;0};{set;~forcedupdate;1}}
{set;~dataupdate;0}

{function;initarray;
  {if;{isarray;{get;{params;0}}};==;false;
    {set;{params;0};[]}
  }
}

{//; afk update text}
{function;afktext;
  {if;{length;{params}};
    {set;~now;{time;X}}
    {set;~response;The following {if;{length;{params}};{length;{params}}{space}}members are marked as being AFK and their expected return time:{newline}{newline}{regexreplace;{foreach;~uid;{params};> {usernick;{get;~uid}}:{space}{substring;{get;_{get;~uid}_afk_msg};0;100}{newline}{set;~afkX;{time;X;{get;_{get;~uid}_afk}}}{set;~ago;{trim;{execcc;timediff;twodivs;{get;~now};{get;~afkX}}}}{if;{math;-;{get;~now};{get;~afkX}};<;0;will return **in {get;~ago}**.;said they'd return **{get;~ago} ago**.}{newline}};/, $/;.}}
  ;
    {set;~response;No members are currently marked as being AFK.}
  }
}

{//; reminder update text}
{function;remindertext;
  {set;~lparams;{params;0}}
  {if;{length;{get;~lparams}};<;2;{set;~response;Nothing to remind};
  {set;~now;{time;X}}
{set;~response;**Active Reminders for {get;~lparams;0}**
```java
ID |  Remain  | Destination | Content
--------------------------------------------------
{for;~i;1;<;{length;{get;~lparams}};{set;~rcid;{get;~lparams;{get;~i}}}{realpad;{get;~i};2;{space};left} | {realpad;{trim;{execcc;timediff;twodivsshort;{get;~now};{get;{get;~rcid};2}}};8;{space};left} | {set;~chanid;{get;{get;~rcid};3}}{realpad;{if;{get;~chanid};==;DM;DM;{substring;#{channelname;{get;~chanid}};0;11}};11;{space};left} | {execcc;removementions;{get;{get;~rcid};4}}{newline}}
```}
}}

{function;update_chan_msg;
  {//;void;{send;{channelid;testing-playground};testing {params;2}}}
  {set;~update_chan_msg_success;0}
  {if;{paramslength};==;3;
    {set;~bmsgtime;{messageedittime;{params;0};{get;_{params;0}_msgid};X}}
    {if;{logic;||;{bool;{params;2};!=;0};{bool;{math;+;{messageedittime;{params;0};{get;_{params;0}_msgid};X};900};<;{time;X}}};
      {if;0{get;_{params;0}_msgid};
        {if;{messagetext;{params;0};{get;_{params;0}_msgid}};!=;{params;1};
          {edit;{params;0};{get;_{params;0}_msgid};{params;1}}
          {if;{get;~bmsgtime};==;{messageedittime;{params;0};{get;_{params;0}_msgid};X};
            {set;{get;_nextbgrun};{math;+;{time;X};30}}
		  ;
  		    {set;_{params;0}_nextupdate;{math;+;{time;X};300}}
			{set;~update_chan_msg_success;1}
          }
        }
      ;
        {set;_{params;0}_msgid;{send;{params;0};{params;1}}}
        {if;{messagetext;{params;0};{get;_{params;0}_msgid}};!=;{params;1};
          {set;{get;_nextbgrun};{math;+;{time;X};30}}
        ;
          {set;_{params;0}_nextupdate;{math;+;{time;X};300}}
		  {set;~update_chan_msg_success;1}
        }
      }
	}
  }
}

{set;~priority;[]}
{push;{get;~priority};{messageedittime;{get;~wsa-afk-chan};{get;_{get;~wsa-afk-chan}_msgid};X}_wsa_afk}
{push;{get;~priority};{messageedittime;{get;~wsa-remind-chan};{get;_{get;~wsa-remind-chan}_msgid};X}_wsa_remind}
{push;{get;~priority};{messageedittime;{get;~wsb-afk-chan};{get;_{get;~wsb-afk-chan}_msgid};X}_wsb_afk}
{push;{get;~priority};{messageedittime;{get;~wsb-remind-chan};{get;_{get;~wsb-remind-chan}_msgid};X}_wsb_remind}
{sort;{get;~priority}}

{for;~ind;0;<;4;
  {//; WSA afk update}
  {if;{get;~priority;{get;~ind}};endswith;_wsa_afk;
    {//; check to see if underlying data has changed. If changed, forced an update}
    {func.initarray;_{get;~wsa-afk-chan}_data}
	{if;{logic;||;{bool;{get;~afk-wsa-uid};!=;{get;_{get;~wsa-afk-chan}_data}};{bool;{get;~forcedupdate};==;1}};{set;~dataupdate;1};{set;~dataupdate;0}}
	
    {func.afktext;{get;~afk-wsa-uid}}
    {func.update_chan_msg;{get;~wsa-afk-chan};{get;~response};{get;~dataupdate}}
	{if;{get;~update_chan_msg_success};==;1;{set;_{get;~wsa-afk-chan}_data;{get;~afk-wsa-uid}}}
  }
  {//; WSA reminder updates}
  {if;{get;~priority;{get;~ind}};endswith;_wsa_remind;
    {set;~remindertextparamA;[]}
    {push;{get;~remindertextparamA};WSA}
    {set;~remindertextparamA;{concat;{get;~remindertextparamA};{execcc;remind;listchans;{execcc;wsachans}}}}
	
	{//; check to see if underlying data has changed. If changed, forced an update}
	{func.initarray;_{get;~wsa-remind-chan}_data}
	{if;{logic;||;{bool;{get;~remindertextparamA};!=;{get;_{get;~wsa-remind-chan}_data}};{bool;{get;~forcedupdate};==;1}};{set;~dataupdate;1};{set;~dataupdate;0}}
	
    {func.remindertext;{get;~remindertextparamA}}
    {func.update_chan_msg;{get;~wsa-remind-chan};{get;~response};{get;~dataupdate}}
	{if;{get;~update_chan_msg_success};==;1;{set;_{get;~wsa-remind-chan}_data;{get;~remindertextparamA}}}
  }

  {//; WSB afk update}
  {if;{get;~priority;{get;~ind}};endswith;_wsb_afk;
    {//; check to see if underlying data has changed. If changed, forced an update}
    {func.initarray;_{get;~wsb-afk-chan}_data}
	{if;{logic;||;{bool;{get;~afk-wsb-uid};!=;{get;_{get;~wsb-afk-chan}_data}};{bool;{get;~forcedupdate};==;1}};{set;~dataupdate;1};{set;~dataupdate;0}}
	
    {func.afktext;{get;~afk-wsb-uid}}
    {func.update_chan_msg;{get;~wsb-afk-chan};{get;~response};{get;~dataupdate}}
	{if;{get;~update_chan_msg_success};==;1;{set;_{get;~wsb-afk-chan}_data;{get;~afk-wsb-uid}}}
  }
  {//; WSB reminder updates}
  {if;{get;~priority;{get;~ind}};endswith;_wsb_remind;
    {set;~remindertextparamB;[]}
    {push;{get;~remindertextparamB};WSB}
    {set;~remindertextparamB;{concat;{get;~remindertextparamB};{execcc;remind;listchans;{execcc;wsbchans}}}}
	
	{//; check to see if underlying data has changed. If changed, forced an update}
	{func.initarray;_{get;~wsb-remind-chan}_data}
	{if;{logic;||;{bool;{get;~remindertextparamB};!=;{get;_{get;~wsb-remind-chan}_data}};{bool;{get;~forcedupdate};==;1}};{set;~dataupdate;1};{set;~dataupdate;0}}
	
    {func.remindertext;{get;~remindertextparamB}}
    {func.update_chan_msg;{get;~wsb-remind-chan};{get;~response};{get;~dataupdate}}
	{if;{get;~update_chan_msg_success};==;1;{set;_{get;~wsb-remind-chan}_data;{get;~remindertextparamB}}}
  }
}